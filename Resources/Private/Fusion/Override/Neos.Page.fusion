prototype(Neos.Neos:Page) {
    @context.hasChanges = ${node.context.inBackend == true && EditConflictPrevention.hasChangesInOtherWorkspaces(documentNode)}

    head {
        editConflictWarning = Neos.Fusion:Tag {
            @if.inBackend = ${node.context.inBackend == true}
            @if.hasChanges = ${hasChanges == true}
            @position = 'after stylesheets'

            tagName = 'link'
            attributes {
                href = Neos.Fusion:ResourceUri {
                    path = 'resource://PunktDe.EditConflictPrevention/Public/Styles/Backend.css'
                }

                type = 'text/css'
                rel = 'stylesheet'
            }
        }
    }

    editConflictWarning = Neos.Fusion:Component {

        @position = 'before body'
        @if.inBackend = ${node.context.inBackend == true}
        @if.hasChanges = ${hasChanges == true}

        changes = Neos.Fusion:Loop {
            items = ${EditConflictPrevention.calculateChangesInOtherWorkspaces(documentNode)}

            itemRenderer = Neos.Fusion:Fragment {

                subject = ${item.nodeLabel}

                renderer = afx`
                    <tr>
                        <td><PunktDe.EditConflictPrevention:Components.Atom.ChangeDate /></td>
                        <td><PunktDe.EditConflictPrevention:Components.Atom.ChangeType /></td>
                        <td>{props.subject}</td>
                        <td><PunktDe.EditConflictPrevention:Components.Molecule.Workspace /></td>
                    </tr>
                `
            }

            @if.hasChanges = ${hasChanges == true}
        }

        renderer = afx`
            <div class="editconflict-canvas">
                <div class="editconflict-header">
                    <h1>Caution</h1>
                    <div>This page has changes in other workspaces</div>
                </div>
                <table class="editconflict-table">
                    <tr>
                        <th>Date</th>
                        <th>Type</th>
                        <th>Node</th>
                        <th>Workspace</th>
                    </tr>
                    {props.changes}
                </table>
            </div>
        `
    }
}
